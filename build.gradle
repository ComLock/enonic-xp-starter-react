plugins {
  id 'java'
  id 'com.enonic.xp.app' version '1.0.15'
  id 'com.moowork.node' version '1.2.0'
}


task wrapper(type: Wrapper) {
  gradleVersion = '4.0.1'
  //distributionType = 'all'
}


repositories {
  mavenLocal()
  jcenter()
  xp.enonicRepo()
}


app {
  name = project.ext.appName
  displayName = project.ext.displayName
  vendorName = 'Enonic AS'
  vendorUrl = 'http://enonic.com'
  systemVersion = "${xpVersion}"
}


dependencies {
  compile "com.enonic.xp:core-api:${xpVersion}"
  compile "com.enonic.xp:portal-api:${xpVersion}"
  include "com.enonic.xp:lib-content:${xpVersion}"
  include "com.enonic.xp:lib-portal:${xpVersion}"
} // dependencies


node {
  version = '8.9.3'

  // Base URL for fetching node distributions (change if you have a mirror).
  distBaseUrl = 'https://nodejs.org/dist'

  // If true, it will download node using above parameters.
  // If false, it will try to use globally installed node.
  // NOTE: If you have AVN working you could probably set this to false.
  download = true

  npmVersion = '5.5.1'

  yarnVersion = '1.3.2'

} // node


task watch(type: NodeTask) {
  script = file('node_modules/webpack-cli/bin/cli.js')
  args = [
    '--color',
    '--hide-modules',
    '--watch'
  ]
}


task webpack(type: NodeTask) {
  script = file('node_modules/webpack-cli/bin/cli.js')
  args = [
    '--color',
    '--hide-modules',
    //'--bail' // Depends on whether you want to see all errors at once.
  ]
}


webpack.dependsOn yarn_install
webpack.mustRunAfter yarn_install

build.dependsOn webpack
build.mustRunAfter webpack


tasks.withType(Copy) {
  includeEmptyDirs = false
}


sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            //exclude '*'
            exclude '*.js'
            //include '*.svg'
            //include '*.xml'
        }
    }
}


task deploy(type: YarnTask, overwrite: true) {
  args = [
    'deploy'
  ]
}
